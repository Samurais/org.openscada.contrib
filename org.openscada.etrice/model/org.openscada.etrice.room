RoomModel org.openscada.etrice {
	
	import room.basic.types.* from "platform:/resource/org.eclipse.etrice.modellib.java/models/Types.room"
	
 	ActorClass ServerDataItem {
		
		Interface {
			Port server : ServerItemProtocol
		}
		Structure {
			usercode1{
				"import org.openscada.da.server.common.chain.DataItemInputOutputChained;"
				"import org.openscada.core.VariantEditor;"
			}
			usercode2 {
				"private HiveWrapper hiveWrapper;"
				"private DataItemInputOutputChained item;"
			}
			external Port server
		}
		Behavior {
			Operation initUser () {
				"this.hiveWrapper =  new HiveWrapper ();"
			}
			Operation destroyUser () {
				"this.hiveWrapper.dispose ();"
				"this.hiveWrapper = null;"
			}
			StateMachine {
				Transition init: initial -> unconfigured { }
				Transition doRegister: unconfigured -> registered {
					triggers {
						<register: server>
					}
					action {
						"this.item = this.hiveWrapper.getHive ().registerItem ( itemName, this.server);"
					}
				}
				Transition doUpdate : registered -> registered {
					triggers {
						<update: server>
					}
					action{
						"this.item.updateData ( VariantEditor.toVariant ( value ), null, null );"
					}
				}
				State registered
				State unconfigured
			}
		}
	}
	
	ProtocolClass ServerItemProtocol {
		incoming {
			Message register ( itemName : string )
			Message update ( value : string )
		}
		outgoing {
			Message writeCall ( value : string )
		}
	}
}